import pandas as pd

window = 500000

localrules:
    index_vcf,

include: "tensorqtl.smk"

rule all:
    input:
        "data/Eye.cis_qtl_signif.txt.gz",
        "data/Eye.aFC.txt",


rule index_vcf:
    """Generate an index file for a VCF file."""
    input:
        "{base}.vcf.gz"
    output:
        "{base}.vcf.gz.tbi"
    shell:
        "tabix -p vcf {input}"


rule aFC:
    """Get effect size (allelic fold change) for top association per gene and all significant eQTLs."""
    input:
        vcf = "data/genotype/eyes.vcf.gz",
        vcfi = "data/genotype/eyes.vcf.gz.tbi",
        bed = "data/Eye.expr.log2.bed.gz",
        bedi = "data/Eye.expr.log2.bed.gz.tbi",
        qtl = "data/Eye.cis_qtl.txt.gz",
        qtl_indep = "data/Eye.cis_independent_qtl.txt.gz",
        covar = "data/covar.txt"
    output:
        "data/Eye.aFC.txt"
    resources:
        walltime = 12
    shell:
        """
        python3 ~/tools/aFC/aFC.py \
        --vcf {input.vcf} \
        --pheno {input.bed} \
        --qtl <(python3 src/prepare_qtl_for_afc.py {input.qtl} {input.qtl_indep}) \
        --cov {input.covar} \
        --log_xform 1 \
        --output {output}
        """
